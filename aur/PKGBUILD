# Maintainer: NeptuneNepgear <ultra.neppers@gmail.com>
# Contributor: qwertyuiopleowo <newleowoxl@gmail.com>

pkgname=rust4diva-git
pkgver=r208.dc5e3d3
pkgrel=1
pkgdesc="Project Diva Mod Manager written in rust"
arch=('x86_64' 'aarch64')
url="https://github.com/R3alCl0ud/Rust4Diva"
license=('GPL-3.0')
depends=('libarchive')
makedepends=('cargo' 'git')
replaces=('Rust4Diva-git')
options=(!debug !lto)
source=("${pkgname%-git}::git+https://github.com/R3alCl0ud/Rust4Diva.git")
sha256sums=('SKIP')

pkgver() {
	cd "${pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
	cd "${pkgname%-git}"
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	cargo build --frozen --release --all-features
}

package() {
	cd "${pkgname%-git}"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/rust4diva"
	install -Dm0755 -t "$pkgdir/usr/share/icons/" "assets/rust4diva.png"
	install -Dm0755 -t "$pkgdir/usr/share/applications/" "assets/Rust4Diva.desktop"
}
